plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

dependencies {
    implementation(jarJar("one.pkg.velocity_rc:velocity-native:3.4.0-SNAPSHOT")) {
        exclude group: 'io.netty'
    }
    implementation(jarJar("com.google.auto.service:auto-service-annotations:1.1.1"))
    implementation(jarJar("com.google.auto.service:auto-service:1.1.1"))
    implementation(jarJar("org.yaml:snakeyaml:2.4"))
    implementation(jarJar("com.github.bsommerfeld.jshepherd:core:${config_api_version}"))
    implementation(jarJar("com.github.bsommerfeld.jshepherd:yaml:${config_api_version}"))
}

tasks.jarJar {
    enabled = true
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }