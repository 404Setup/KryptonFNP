plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '[6.0.35,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}
base {
    archivesName = "${mod_name}-forge-${minecraft_version}"
}

configurations {
    shade
    compile.extendsFrom shade
}

mixin {
    config("${mod_id}.mixins.json")
    //config("${mod_id}.forge.mixins.json")
}

jar {
    manifest {
        attributes["MixinConfigs"] = "${mod_id}.mixins.json"//,${mod_id}.forge.mixins.json"
    }
}

jarJar.enable()

tasks.named('jarJar') {
    archiveClassifier = ''
}

jarJar.dependencies {
    exclude(dependency('com.google.guava:guava:32.0.1-jre'))
    exclude(dependency('org.yaml:snakeyaml:2.3'))

}

jar.finalizedBy('jarJar')

minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${parchment_minecraft}"

    copyIdeResources = true //Calls processResources when in dev

    reobf = false // Forge 1.20.6+ uses official mappings at runtime, so we shouldn't reobf from official to SRG

    // Automatically enable forge AccessTransformers if the file exists
    // This location is hardcoded in Forge and can not be changed.
    // https://github.com/MinecraftForge/MinecraftForge/blob/be1698bb1554f9c8fa2f58e32b9ab70bc4385e60/fmlloader/src/main/java/net/minecraftforge/fml/loading/moddiscovery/ModFile.java#L123
    // Forge still uses SRG names during compile time, so we cannot use the common AT's
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at
    }

    runs {
        client {
            workingDirectory file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory file('runs/data')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            mods {
                modDataRun {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor("org.spongepowered:mixin:0.8.7:processor")
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.4.1")) {
        jarJar.ranged(it, "[0.4.1,)")
    }
    implementation(jarJar("com.google.auto.service:auto-service-annotations:1.1.1")) {
        jarJar.ranged(it, "[1.1.1,)")
        exclude group: "org.checkerframework"
        exclude group: 'com.google.guava'
    }
    implementation(jarJar("com.google.auto.service:auto-service:1.1.1")) {
        jarJar.ranged(it, "[1.1.1,)")
        exclude group: "org.checkerframework"
        exclude group: 'com.google.guava'
    }
    implementation(jarJar("org.yaml:snakeyaml:2.4")) {
        jarJar.ranged(it, "[2.4,)")
    }
    implementation(jarJar("com.github.bsommerfeld.jshepherd:core:${config_api_version}")) {
        jarJar.ranged(it, "[${config_api_version},)")
    }
    implementation(jarJar("com.github.bsommerfeld.jshepherd:yaml:${config_api_version}")) {
        jarJar.ranged(it, "[${config_api_version},)")
        exclude group: 'org.yaml'
    }
    implementation(jarJar("one.pkg.velocity_rc:velocity-native:3.4.0-SNAPSHOT")) {
        jarJar.ranged(it, "[3.4.0-SNAPSHOT,)")
        exclude group: 'io.netty'
        exclude group: 'com.google.guava'
    }

    // Forge's hack fix
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            fg.component(it)
        }
    }
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

